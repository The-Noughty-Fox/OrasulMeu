plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.kotlinAndroid)
    alias(libs.plugins.hiltAndroid)
    alias(libs.plugins.crashlytics)
    alias(libs.plugins.kotlin.serialization)

    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'com.thenoughtfox.orasulmeu'
    compileSdk 34

    defaultConfig {
        applicationId "com.thenoughtfox.orasulmeu"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources = true
            signingConfig signingConfigs.releaseConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "OrasulMeu"

            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
        }

        debug {
            minifyEnabled false
            shrinkResources = false
            debuggable true
            resValue "string", "app_name", "OrasulMeu Dev"
            signingConfig signingConfigs.releaseConfig

            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.4'
    }

    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

//    sourceSets {
//        main {
//            java.srcDirs = ['build/generated/api/src/main/java']
//        }
//    }
}

dependencies {
    implementation project(":generated:api")
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation libs.androidx.viewmodel.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.recyclerview

    //Coroutines
    implementation libs.coroutines.core
    implementation libs.coroutines.android

    implementation libs.play.services.maps

    //Lifecycle
    implementation libs.lifecycle.viewmodel
    implementation libs.lifecycle.livedata
    implementation libs.lifecycle.service

    //Permission
    implementation libs.permission.dispetcher.ktx
    implementation libs.permission.dispetcher
    kapt libs.permission.dispetcher.processor

    //Retrofit
    implementation libs.retrofit
    implementation libs.retrofit.converter.gson
    implementation libs.okhttp3.logging.interceptor

    //Hilt
    implementation libs.hilt.android
    implementation libs.androidx.hilt.navigation.compose
    kapt libs.hilt.compiler

    //Joda time
    implementation libs.joda.time

    //Logger
    implementation libs.timber

    //Google Auth
    implementation libs.play.services.auth

    //GSON
    implementation libs.gson

    //Firebase
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.analytics.ktx
    implementation libs.firebase.crashlytics

    //Desugar java library
    coreLibraryDesugaring libs.desugar.jdk.libs

    //Noughty lib
    implementation libs.noughty.authentication.lib.android

    //ViewBinding
    implementation libs.view.binding.property.delegate
    implementation libs.androidx.ui.viewbinding


    //Fragment
    implementation libs.androidx.fragment.ktx

    // navigation
    implementation(libs.navigation.compose)

    // serialization
    implementation(libs.kotlinx.serialization.json)

    //Image
    implementation libs.coil.compose

    //Camera
    implementation libs.camerax.core
    implementation libs.camerax.view
    implementation libs.camerax.lifecycle
    implementation libs.camerax.camera2

    //WindowsInset
    implementation libs.insetter

    //Mapbox
    implementation libs.mapbox
    implementation libs.mapbox.search

    implementation libs.play.services.location

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest
}